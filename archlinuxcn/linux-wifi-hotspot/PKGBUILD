# Maintainer: Integral <integral@member.fsf.org>
# Contributor: Mark Wagie <mark dot wagie at proton dot me>
# Contributor: Dimitris Kiziridis <ragouel at outlook dot com>

pkgname=linux-wifi-hotspot
pkgver=4.7.2
pkgrel=2
epoch=1
pkgdesc="Feature-rich wifi hotspot creator"
arch=('x86_64' 'aarch64')
url="https://github.com/lakinduakash/${pkgname}"
license=('BSD-2-Clause')
depends=('gtk3' 'hostapd' 'iproute2' 'iw' 'procps-ng' 'qrencode')
makedepends=('git')
optdepends=("dnsmasq: For 'NATed' or 'None' Internet sharing method"
	"iptables: For 'NATed' or 'None' Internet sharing method"
	'haveged: For random MAC generation'
	'wireless_tools: if iw cannot recognize your adapter')
provides=('wihotspot' 'create_ap')
conflicts=('wihotspot' 'create_ap')
backup=('etc/create_ap.conf')
install="${pkgname}.install"
source=("git+${url}.git#tag=v${pkgver}")
sha256sums=('648414d5ea809e9a7eacacbcebca61c8d61fd01d86677d23340501c817b8587c')

prepare() {
	cd "${pkgname}/"
	git cherry-pick -n a3fce4b3ee9371eeb7b300fa7e9f291d93986db3
}

build() {
	make -C "${pkgname}/"
}

package() {
	cd "${pkgname}/"
	make DESTDIR="${pkgdir}" install

	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
	install -d "${pkgdir}/usr/share/doc/${pkgname}/"
	cp -r docs/* "${pkgdir}/usr/share/doc/${pkgname}/"
}
