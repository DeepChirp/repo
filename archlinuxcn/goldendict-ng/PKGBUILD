# Maintainer: Integral <integral@member.fsf.org>

pkgname=goldendict-ng
pkgver=25.10.1
pkgrel=2
pkgdesc="The next generation GoldenDict (Supports Qt WebEngine & Qt6)."
arch=('i686' 'x86_64' 'aarch64')
url="https://github.com/xiaoyifang/${pkgname}"
license=('GPL-3.0-or-later')
depends=(
	'hunspell'
	'libvorbis'
	'libxtst'
	'lzo'
	'zlib'
	'xz'
	'libeb'
	'tomlplusplus'
	'fmt'
	'opencc'
	'xapian-core'
	'libzim'
	'qt6-base'
	'qt6-svg'
	'qt6-multimedia'
	'qt6-webengine'
	'qt6-speech'
	'qt6-5compat'
)
makedepends=('cmake' 'qt6-tools')
conflicts=('goldendict')
provides=('goldendict')
replaces=('goldendict-svn' 'goldendict-git-opt' 'goldendict-webengine-git')
_commit_id=85f9a3d5
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}-Release.${_commit_id}.tar.gz")
sha256sums=('bbf3c23dd0a3a17fa4d083144a579aea9f766d99e3b995ed1c04760665001c47')

build() {
	cmake -B build \
		-S "${pkgname}-${pkgver}-Release.${_commit_id}" \
		-DCMAKE_BUILD_TYPE=None \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DUSE_SYSTEM_FMT=ON \
		-DUSE_SYSTEM_TOML=ON

	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
}
