# Maintainer: Coelacanthus <coelacanthus@archlinuxcn.org>
# Contributor: Dominic Meiser [git at msrd0 dot de]

pkgname=probe-rs
pkgver=0.29.1
pkgrel=3
pkgdesc='A collection of on chip debugging tools to communicate with microchips.'
url='https://probe.rs'
arch=('aarch64' 'i686' 'x86_64')
license=('Apache-2.0' 'MIT')
depends=(
    'gcc-libs'
    'systemd-libs'
)
makedepends=(
    'cargo'
    'cargo-auditable'
    'cmake'
    'git'
)
provides=(
    'cargo-embed'
    'cargo-flash'
    'rtthost'
    'smoke_tester'
    'target-gen'
    'xtask'
)
conflicts=(
    'cargo-embed'
    'cargo-flash'
    'rtthost'
    'smoke_tester'
    'target-gen'
    'xtask'
)
source=(
    "$pkgname::git+https://github.com/probe-rs/probe-rs.git#tag=v$pkgver"
    "$pkgname-69-probe-rs.rules::https://github.com/probe-rs/webpage/raw/refs/heads/master/public/files/69-probe-rs.rules"
)
b2sums=('57c2c5de9b3581c50547b833b96b4a5924ee7ddb93d1f7cd9aa24610301055687a40815b38988711f85910256f98103ecc13446bfa84d36863de59cba9a208c5'
        '155e37902a17198be77c3aa1f1f9407ab2112083606630f5115ca27261a47345fb646770ff44d90cd258d8bc187d4b2a0f5620b3bc0eadaddd5a845d4226a45f')

prepare() {
	cd "$pkgname"

	export RUSTUP_TOOLCHAIN=stable
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "$pkgname"
	
	export RUSTUP_TOOLCHAIN=stable
	export CARGO_TARGET_DIR=target
	CFLAGS+=" -ffat-lto-objects"

	cargo auditable build \
		--frozen \
		--release \
        --all-features
}

check() {
    export RUSTUP_TOOLCHAIN=stable
    cargo test \
        --workspace \
        --frozen \
        --all-features \
}

package() {
	cd "$pkgname"
    find target/release \
        -maxdepth 1 \
        -executable \
        -type f \
        -exec install -Dm0755 -t "$pkgdir/usr/bin/" {} +
    SHELL=zsh  target/release/probe-rs complete --shell zsh install
    SHELL=bash target/release/probe-rs complete --shell bash install
    SHELL=fish target/release/probe-rs complete --shell fish install > probe-rs.fish

	install -Dm644 ~/.zfunc/_probe-rs -t "$pkgdir/usr/share/zsh/site-functions"
	install -Dm644 ~/.local/share/bash-completion/completions/probe-rs.bash -t "$pkgdir/usr/share/bash-completion/completions/"
	install -Dm644 probe-rs.fish -t "$pkgdir/usr/share/fish/vendor_completions.d"
	install -Dm644 "$srcdir/$pkgname-69-probe-rs.rules" "$pkgdir/usr/lib/udev/rules.d/69-probe-rs.rules"
}

# vim: set sts=4 ts=4 sw=4 et:
