pkgname=julia-git-enzyme_jll-src
pkgver=0.0.198+0
_commit=4c11e3ed064fba535ea2d99006176b4963a1e833
pkgrel=1
pkgdesc="Enzyme_jll.jl"
url="https://github.com/JuliaBinaryWrappers/Enzyme_jll.jl.git"
arch=(aarch64 armv7h x86_64)
license=('MIT')
makedepends=(git julia-pkg-scripts llvm)
depends=(julia-git enzyme-julia)
source=("git+https://github.com/JuliaBinaryWrappers/Enzyme_jll.jl.git#commit=$_commit"
        jll.toml.in)
sha256sums=('eab4f9927596054aa5b930989dba8c34bd01f273c04b120ad0bca3b382b7792f'
            '7fe88d60082a7d6de7f775f36bef4247c04be3c9bd3f3c923851ebcf5ef86a84')

build() {
  _llvm_ver=$(llvm-config --version)
  [[ $_llvm_ver =~ ^([0-9]*).* ]]
  llvm_maj_ver=${BASH_REMATCH[1]}

  sed -e "s/@LLVM_VER@/${llvm_maj_ver}/g" jll.toml.in > jll.toml

  cd Enzyme_jll.jl

  julia /usr/lib/julia/julia-gen-jll.jl Enzyme_jll ../jll.toml
}

package() {
  cd Enzyme_jll.jl

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh Enzyme_jll "${pkgdir}" "${pkgname}" julia-git
}
