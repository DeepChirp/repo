# Maintainer: Integral <integral@member.fsf.org>
# Contributor: Philip Jones <philj56@gmail.com>

pkgname=tofi
pkgver=0.9.1
pkgrel=3
pkgdesc="Tiny rofi / dmenu replacement for wlroots-based Wayland compositors."
arch=("x86_64")
url="https://github.com/philj56/${pkgname}"
license=("MIT")
depends=("freetype2" "harfbuzz" "cairo" "pango" "wayland" "libxkbcommon" "glib2")
makedepends=("meson" "scdoc" "wayland-protocols")
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('a14ab5ecf2c6e1ecb0ec3366c436140aa422995d464de513e81e454df0f303fc9661b534a3f40df4f14897629cd0cc299bb449482b676fbf254002731ac02231')

build() {
	arch-meson "${pkgname}-${pkgver}" build
	meson compile -C build
}

check() {
	meson test -C build --print-errorlogs
}

package() {
	meson install -C build --destdir "${pkgdir}"
}
