# Copyright (C) 2019 Denis 'GNUtoo' Carikli <GNUtoo@cyberdimension.org>
# This program is free software: you can redistribute it and/or modify
# it under the terms of the CC0 1.0 License.

pkgname=qdl
pkgver=2.2
pkgrel=1
pkgdesc="Tool to communicate with Qualcomm System On a Chip bootroms to install or execute code"
arch=(armv7h aarch64 i686 x86_64)
url='https://github.com/linux-msm/qdl'
license=('BSD3')
depends=(libxml2.so zlib xz libicui18n.so libicuuc.so libicudata.so libusb-1.0.so)
source=("https://github.com/andersson/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('b06669d80e8d40aab33f50418cb3894d9eba1442fc141961a9a92799c724c37bdfa10706a2fbe376b7bfdfe49b608cfbda3dfd7814f1c1cf41fe44af101c9c43')

build(){
  cd "${srcdir}/${pkgname}-${pkgver}"
  make CFLAGS="$CFLAGS -include stdlib.h $(pkgconf libusb-1.0 --cflags) -O2 -Wall -g $(xml2-config --cflags)"
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"
  make prefix="/usr" DESTDIR="${pkgdir}" install

  # Package license
  install -d "${pkgdir}/usr/share/licenses/${pkgname}/"
  install LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/"

  # Package documentation
  install -d "${pkgdir}/usr/share/doc/${pkgname}/"
  install README.md "${pkgdir}/usr/share/doc/${pkgname}/"
}
