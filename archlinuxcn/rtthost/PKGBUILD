# Maintainer: Coelacanthus <coelacanthus@archlinuxcn.org>
# Contributor: Dominic Meiser [git at msrd0 dot de]

_crate="rtthost"
pkgname="rtthost"
pkgver=0.29.1
pkgrel=2
pkgdesc='RTT (Real-Time Transfer) client.'
url='https://probe.rs'
arch=('aarch64' 'i686' 'x86_64')
license=('MIT')
depends=(
    'gcc-libs'
    'systemd-libs'
)
makedepends=(
    'cargo'
    'cargo-auditable'
    'cmake'
)
source=(
    "$pkgname-$pkgver.tar.gz::https://static.crates.io/crates/$_crate/$_crate-$pkgver.crate"
)
b2sums=('6b8f5b947ca88a0ca0327bc7c6d4eb2d4f7385e401e03330295642213964b958b987d32467d0fcdbe0550eb3154cc8aa22d62cf9f98c81d4b7d97c711046cb74')


prepare() {
	cd "$_crate-$pkgver"

	export RUSTUP_TOOLCHAIN=stable
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "$_crate-$pkgver"
	
	export RUSTUP_TOOLCHAIN=stable
	export CARGO_TARGET_DIR=target
	CFLAGS+=" -ffat-lto-objects"

	cargo auditable build \
		--frozen \
		--release \
        --all-features
}

package() {
	cd "$_crate-$pkgver"
    find target/release \
        -maxdepth 1 \
        -executable \
        -type f \
        -exec install -Dm0755 -t "$pkgdir/usr/bin/" {} +
}

# vim: set sts=4 ts=4 sw=4 et:
